import "strings"

"agentcore-memory": {
	alias: ""
	annotations: {}
	attributes: {
		workload: type: "autodetects.core.oam.dev"
		status: {
			healthPolicy: "isHealth: *( context.output.status.atProvider.id != \"\" ) | false"
			customStatus: #"""
				message: *("memoryId: " + context.output.status.atProvider.id) | "provisioning"
				memoryId: *context.output.status.atProvider.id | ""
				"""#
		}
	}
	description: "AgentCore Memory provisioned via Crossplane managed resource with IAM policy"
	labels: {}
	type: "component"
}

template: {
	let _autoName = strings.Replace(context.namespace + "_" + context.name, "-", "_", -1)

	output: {
		apiVersion: "bedrockagentcore.aws.upbound.io/v1beta1"
		kind:       "Memory"
		metadata: name: context.name
		spec: {
			forProvider: {
				name:                parameter.memoryName
				region:              parameter.region
				description:         parameter.description
				eventExpiryDuration: parameter.eventExpiryDuration
			}
			providerConfigRef: name: "default"
		}
	}

	outputs: "\(context.name)-iam-policy": {
		apiVersion: "iam.aws.upbound.io/v1beta1"
		kind:       "Policy"
		metadata: name: "\(context.appName)-\(context.name)-iam-policy"
		spec: {
			forProvider: {
				name: "\(context.appName)-\(context.name)-iam-policy"
				policy: """
					{
					  "Version": "2012-10-17",
					  "Statement": [
					    {
					      "Effect": "Allow",
					      "Action": ["bedrock-agentcore:*"],
					      "Resource": "*"
					    },
					    {
					      "Effect": "Allow",
					      "Action": [
					        "bedrock:InvokeModel",
					        "bedrock:InvokeModelWithResponseStream"
					      ],
					      "Resource": "*"
					    }
					  ]
					}
					"""
			}
			providerConfigRef: name: "default"
		}
	}

	parameter: {
		// +usage=Memory name in AWS (must match ^[a-zA-Z][a-zA-Z0-9_]{0,47}$). Defaults to <namespace>_<componentName>
		memoryName: *_autoName | string
		// +usage=AWS region. Defaults to the cluster's region so the same OAM Application
		// is portable across regions; override only for a cross-region memory store.
		region: *"{{ .Values.global.awsRegion }}" | string
		// +usage=Description of the memory
		description: *"AgentCore Memory" | string
		// +usage=Number of days after which events expire (3-365)
		eventExpiryDuration: *30 | int
	}
}
